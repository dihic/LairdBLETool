@echo off
rem **********************************************************************
rem
set hex1=ITSE01020_08_BL600Phase1COMBINED_v1_5_70_0
set softdev=ITSE01021_08_s110_nrf51822_6.0.0_softdevice
rem
rem **********************************************************************
cls

set lic=yes

if "%1" == "" goto :noargs
if NOT "%1" == "unlicensed" goto :unknownarg
REM Command line arg == "unlicensed" 
set lic=no

:noargs
echo .
echo Programming, please wait it will take up to 20 seconds ....
echo .

set hex2=WormInfo
set app=BL600Phase1All
set wrmstart=0x3FC00
set wrmlen=1024

if NOT "%lic%" == "yes" goto :ignlic
REM *****************************************
REM Licence copied
REM *****************************************

rem --------------------------------
rem  Read Licence
rem --------------------------------
echo ..                Acquiring info
nrfjprog --memrd %wrmstart% --n %wrmlen% --w 8 >%hex2%.txt
if ERRORLEVEL 1 goto nrfjprog_err

rem --------------------------------
rem  Convert to hex file
rem --------------------------------
echo ...
Nrf2Hex %hex2%.txt /B %wrmlen% /I 1 >NUL
if ERRORLEVEL 1 goto nrf2Hex_err

rem --------------------------------
rem  Merge Hex file :
rem --------------------------------
echo ....
mergehex -q -m %hex1%.hex %hex2%.hex -o %app%.hex
goto :contn

REM *****************************************
REM Licence ignored
REM *****************************************
:ignlic

echo ..                ????? Licence ignored ?????
copy %hex1%.hex %app%.hex >NUL

)

REM *****************************************
REM continue
REM *****************************************
:contn
if not exist %app%.hex goto merge_err

rem --------------------------------
rem  Downloading softdevice :
rem --------------------------------
echo .....             Programming stack (please wait)
nrfjprog  --eraseall --programs %softdev%.hex --reset --quiet
echo ......            Programming stack (done)

rem --------------------------------
rem Write the start address of the flash loader application
rem --------------------------------
echo ....... 
nrfjprog --memwr 0x10001014 --val 0x0003F000 --quiet

rem --------------------------------
rem  Downloading application :
rem --------------------------------
echo ........          Programming application  (please wait)
nrfjprog  --program %app%.hex  --reset --quiet
echo .........         Programming application  (done)

rem --------------------------------
rem Write the magic string to address just beyond the app vector table
rem --------------------------------
nrfjprog --memwr 0x140D0 --val 0x7269614C --quiet
echo ..........
nrfjprog --memwr 0x140D4 --val 0x65545F64 --quiet
echo ...........
nrfjprog --memwr 0x140D8 --val 0x6F6E6863 --quiet
echo ............
nrfjprog --memwr 0x140DC --val 0x79676F6C --quiet
echo .............
nrfjprog --memwr 0x140E0 --val 0x364C425F --quiet
echo ..............
nrfjprog --memwr 0x140E4 --val 0x78787878 --quiet
echo ...............

rem --------------------------------
rem Reset the module
rem --------------------------------
echo ................  Resetting module
nrfjprog  --reset --quiet

rem **********************************************************************
echo .................
echo ################################
echo The firmware upgrade is complete
echo ################################
echo .
rem **********************************************************************
if exist %hex2%.hex del %hex2%.hex >NUL
if exist %hex2%.txt del %hex2%.txt >NUL
if exist %app%.hex  del %app%.hex  >NUL
goto :end

:merge_err
echo ?????????????????????????????????????????????????????????????????????
echo FAIL to create %app%.hex using the mergehex utility
echo .
echo On some Windows PC, you will get a 'System Error' dialog box with a 
echo message 'The program can't start because MSVCP100.dll is missing
echo from your computer....'
echo 
echo Visit http://www.microsoft.com/download/en/confirmation.aspx?id=5555
echo to fix the issue.
echo .
echo ?????????????????????????????????????????????????????????????????????
goto :end

:nrf2Hex_err
echo ?????????????????????????????????????????????????????????????????????
echo FAIL
echo .
echo The utility Nrf2Hex has failed. Please contact a Laird representative
echo .
echo ?????????????????????????????????????????????????????????????????????
goto :end

:nrfjprog_err
echo ?????????????????????????????????????????????????????????????????????
echo FAIL
echo .
echo The utility nrfjprog has failed. Please contact a Laird representative
echo .
echo ?????????????????????????????????????????????????????????????????????
goto :end

:unknownarg
echo ?????????????????????????????????????????????????????????????????????
echo FAIL
echo .
echo The command line argument is not recognised
echo Usage :
echo   _DownloadFirmware_vW_X_Y_Z
echo OR
echo   _DownloadFirmware_vW_X_Y_Z  unlicensed
echo .
echo ?????????????????????????????????????????????????????????????????????
goto :end

:end
pause
:abort
